plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

neoForge {
    version = neoforge_version
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})"
        }
        client {
            client()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    // EMI
    runtimeOnly "dev.emi:emi-neoforge:${emi_version}"

    // Cloth Config
    implementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"

    // Reconnectible Chains
    implementation "maven.modrinth:reconnectible-chains:${reconnectible_chains_neoforge_version}"
}

publishMods {
    file = tasks.named("jar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG-LATEST.md").text
    type = STABLE
    displayName = "${mod_name} NeoForge ${minecraft_version} - ${project.version}"
    modLoaders.add("neoforge")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1454717"
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "C1MoWQlb"
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }
}