plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

configurations.configureEach {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$fabric_loader_version")
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // EMI
    modRuntimeOnly "dev.emi:emi-fabric:${emi_version}"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    // Optional Dependencies
    modImplementation "local:connectiblechains-fabric-1.20.1-1.0.0"
    // modImplementation "maven.modrinth:reconnectible-chains:${reconnectible_chains_fabric_version}"

    modCompileOnly "maven.modrinth:station-decoration:Toh7oVVr"
    modCompileOnly "maven.modrinth:minecraft-transit-railway:FABRIC-4.0.0-beta.13+1.20.1"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

publishMods {
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG-LATEST.md").text
    type = STABLE
    displayName = "${mod_name} Fabric ${minecraft_version} - ${project.version}"
    modLoaders.add("fabric")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = ""
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = ""
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }
}