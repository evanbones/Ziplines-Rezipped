plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    // EMI
    modRuntimeOnly "dev.emi:emi-fabric:${emi_version}"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    // Optional Dependencies
    modImplementation "maven.modrinth:reconnectible-chains:${reconnectible_chains_fabric_version}"

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

publishMods {
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG-LATEST.md").text
    type = STABLE
    displayName = "${mod_name} Fabric ${minecraft_version} - ${project.version}"
    modLoaders.add("fabric")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1454717"
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "C1MoWQlb"
        minecraftVersions.add("${minecraft_version}")

        optional { slug = "cloth-config" }
        optional { slug = "reconnectible-chains" }
    }
}